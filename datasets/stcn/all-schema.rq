#+ summary: Fetch actor information from STCN and RMLibrary books and create a new graph. 
#+ endpoint: http://localhost:5820/ga/query
#+ method: GET

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>

CONSTRUCT {
  ?manifestation a schema:Book ;
    schema:author ?NTA_author ;
    schema:contributor ?STCN_contributor ;
    schema:contributor ?RML_contributor ;
    schema:illustrator ?RML_illustrator ;
    schema:publication ?publicationEvent .

  ?publicationEvent a schema:PublicationEvent ;
                      schema:startDate ?date ;
                      sem:hasEarliestBeginTimeStamp ?dateBegin ;
                      sem:hasLatestEndTimeStamp ?dateEnd ;
                      schema:publishedBy ?STCN_printer .

  ?NTA_author a schema:Person .

  ?STCN_printer a schema:Person .

  ?STCN_contributor a schema:Person .

  ?RML_contributor a schema:Person .

  ?RML_illustrator a schema:Person .

} WHERE {
  {
    SELECT * WHERE {

      GRAPH <https://data.goldenagents.org/datasets/ufab7d657a250e3461361c982ce9b38f3816e0c4b/stcn_20200226> {
        ?manifestation a schema:Book .

        BIND(IRI(CONCAT(STR(?manifestation), "-pubEvent")) AS ?publicationEvent)

        OPTIONAL {?manifestation schema:author/schema:author ?NTA_author . }
        OPTIONAL {?manifestation schema:contributor/schema:contributor ?STCN_contributor . }

        OPTIONAL {?manifestation schema:publication/schema:publishedBy ?STCN_printer . }
        OPTIONAL {?manifestation schema:publication/schema:startDate ?date . }
        OPTIONAL {?manifestation schema:publication/sem:hasEarliestBeginTimestamp ?dateBegin . } # Typo in original data
        OPTIONAL {?manifestation schema:publication/sem:hasLatestEndTimestamp ?dateEnd . } # Typo in original data

      }

      GRAPH <https://data.goldenagents.org/datasets/u692bc364e9d7fa97b3510c6c0c8f2bb9a0e5123b/rmlibrary_20210908> {
        ?item a schema:Book ;
              schema:exampleOfWork ?manifestation .

        # These are illustrators for sure
        OPTIONAL {
          ?item schema:contributor ?contributorRole1 .

          ?contributorRole1 a schema:Role ;
                            schema:contributor ?RML_illustrator ;
                            schema:roleName ?roleName1 .

          FILTER(
            ?roleName1 = "graveur"^^xsd:string ||
            ?roleName1 = "illustrator"^^xsd:string ||
            ?roleName1 = "tekenaar"^^xsd:string ||
            ?roleName1 = "houtsnijder"^^xsd:string ||
            ?roleName1 = "etser"^^xsd:string
          )
        }

        # These are just contributors
        OPTIONAL {
          ?item schema:contributor ?contributorRole2 .

          ?contributorRole2 a schema:Role ;
                            schema:contributor ?RML_contributor .

          FILTER NOT EXISTS {
            ?contributorRole2 schema:roleName ?roleName2
          
            FILTER(
              ?roleName2 = "graveur"^^xsd:string ||
              ?roleName2 = "illustrator"^^xsd:string ||
              ?roleName2 = "tekenaar"^^xsd:string ||
              ?roleName2 = "houtsnijder"^^xsd:string ||
              ?roleName2 = "etser"^^xsd:string
            )
          }
        }
      }
    }
  }
}
